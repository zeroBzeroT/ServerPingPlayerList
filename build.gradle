plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
    id 'xyz.jpenilla.run-velocity' version '2.3.1'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'org.zeroBzeroT'
version = "${project.plugin_version}"
description = 'A Velocity plugin for the hover player list in the server list.'

repositories {
    mavenCentral()
    maven { url 'https://repo.velocitypowered.com/snapshots/' }
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
        content {
            includeGroup 'com.velocitypowered'
        }
    }
}

dependencies {
    compileOnly group: 'com.velocitypowered', name: 'velocity-api', version: "${project.velocity_api_version}"
    annotationProcessor group: 'com.velocitypowered', name: 'velocity-api', version: "${project.velocity_api_version}"
    implementation group: 'org.bstats', name: 'bstats-velocity', version: '3.1.0'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks.withType(JavaCompile).configureEach {
    it.sourceCompatibility = it.targetCompatibility = JavaVersion.toVersion(21)
    it.options.release = 21
    it.options.encoding = 'UTF-8'
}

processResources {
    filteringCharset "UTF-8"
    filesMatching("velocity-plugin.json") {
        filter { line -> line.replace("@VERSION@", project.version) }
        filter { line -> line.replace("@DESCRIPTION@", project.description) }
    }
}

// https://imperceptiblethoughts.com/shadow/
shadowJar {
    configurations = [project.configurations.shadow]
    archiveFileName = "${project.name}-${project.version}-velocity.jar"
    destinationDirectory = layout.buildDirectory.dir('dist')

    relocate 'org.bstats',            'org.zeroBzeroT.shaded.bstats'

    // delete unshaded jar
    doLast {
        file(jar.archiveFile).delete()
    }
}
build.finalizedBy(shadowJar)

// test setup
runPaper.disablePluginJarDetection()
tasks {
    runVelocity {
        runDirectory = layout.projectDirectory.dir('run').dir('proxy')
        dependsOn('shadowJar')
        velocityVersion("${project.velocity_api_version}")
        downloadPlugins {
            url("https://github.com/ViaVersion/ViaBackwards/releases/download/5.3.2/ViaBackwards-5.3.2.jar")
            url("https://github.com/ViaVersion/ViaVersion/releases/download/5.3.2/ViaVersion-5.3.2.jar")
            url("https://github.com/zeroBzeroT/AnarchyQueue/releases/download/3.0.8/AnarchyQueue-3.0.8-INDEV.jar")
        }
    }

    tasks.register('runServerMain', runServer.getClass() as Class<Task>) {
        minecraftVersion("${project.minecraft_version}")
        systemProperty('com.mojang.eula.agree', 'true')
        systemProperty('Paper.skipServerPropertiesComments', 'true')
        runDirectory = layout.projectDirectory.dir('run').dir('main')
        dependsOn('shadowJar')
    }

    tasks.register('runServerQueue', runServer.getClass() as Class<Task>) {
        minecraftVersion("${project.minecraft_version}")
        systemProperty('com.mojang.eula.agree', 'true')
        systemProperty('Paper.skipServerPropertiesComments', 'true')
        runDirectory = layout.projectDirectory.dir('run').dir('queue')
        downloadPlugins {
            github("zeroBzeroT", "QueueServerPlugin", "3.0.0-SNAPSHOT", "queueplugin-3.0.0-SNAPSHOT+1.20.6-paper.jar")
            url("https://ci.dmulloy2.net/job/ProtocolLib/723/artifact/build/libs/ProtocolLib.jar")
        }
        dependsOn('shadowJar')
    }
}